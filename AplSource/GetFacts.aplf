 {r}←{cbinfo}GetFacts(cid facts);m;ns;poll;op;int;ok;retried
 :If 0=⎕NC'cbinfo' ⋄ cbinfo←'' 0 ⋄ :EndIf ⍝ do not call back

 facts←,⊆facts
 facts,←(0=≢facts)/allfacts
 :If ∨/m←~facts∊allfacts,⍳≢allfacts
     ('Unknown facts: ',,⍕m/facts)⎕SIGNAL 11
 :EndIf

 (ns←⎕NS'').Facts←facts
 :If poll←0≠int←2⊃cbinfo
     ns.Interval←int⊣op←'PollFacts'
 :Else
     op←'GetFacts'
 :EndIf

 retried←ok←0
 :Repeat
     r←cbinfo SendWait cid(op ns)
     :If ~poll
         :If r≢'CONNECTION CLOSED'
         :AndIf 0≠r[2].⎕NC 'Facts'
             r←r[2].Facts⊣ok←1
         :Else
             retried+←1
         :EndIf
     :EndIf
 :Until ok∨poll∨retried>5

 :If retried>5
     ∘∘∘
 :EndIf
